# Adding Effects

-------

One thing

### Using `NodeProxy` roles

Until very recently I used the 'effects `NodeProxy`' method exclusively, but this method described in the (`NodeProxy roles` helpfile)[http://doc.sccode.org/Reference/NodeProxy_roles.html] is the most direct way to apply an effect to a single proxy in `ProxySpace`. 

With this method you can add an effects UGen to a `NodeProxy` you create by using `List` syntax. I got a question a while back about why you can't address `NodeProxies` in `ProxySpace` as a list (e.g. `~k[0]` being one kick and `~k[1]` being a second) - this is why.

The most basic use of this method to add effects to some patterns I commonly use would be like this:

```supercollider
//basic kick, example from earlier on in the repo
~k = Pbind(\instrument,\bplay,\buf,d["k"][0],\dur,1,\amp,1);
~k.play;
//add a [0] to the name of the proxy
~k[0] = Pbind(\instrument,\bplay,\buf,d["k"][0],\dur,1,\amp,1);
//add some reverb. Note that the input from ~k[0] is received as an argument
~k[1] = \filterIn -> {|in| FreeVerb.ar(in)}
//re-evaluate 'index 0' of the NodeProxy, and effect in [1] will be kept
~k[0] = Pbind(\instrument,\bplay,\buf,d["k"][0],\dur,Pseq([0.25,0.5,0.75],inf),\rate,Pseq([1,1.2,1.3,1.4],inf),\amp,1);
```

This can be used to set up a chain of effects on a `NodeProxy`, feeding into delay, then reverb, then distortion, for example.

The `NodeProxy roles` have some built-in functionality, for example a [`wet/dry`](http://www.indiana.edu/~emusic/etext/studio/chapter2_effects.shtml) control, which mixes the non-effected and effected signal together. Some UGens (for example FreeVerb) have this already present, but many do not.

```supercollider
//slightly more complex starting kick
~k[0] = Pbind(\instrument,\bplay,\buf,d["k"][0],\dur,Pwhite(0.5,1.5).round(0.25),\amp,1,\rate,10);
~k.play;
//create a more complex effects, in this case a bitcrushed modulated delay. Note the original kick disappears
~k[1] = \filterIn -> {|in| Decimator.ar(CombC.ar(in,0.6,0.1+SinOsc.kr(0.1,0,0.05),LFNoise1.kr(0.1).range(0.1,3)),1000)}
//mix in a lot of dry signal so that the original kick is still heard above the effects
~k.set(\wet1,0.3)
```

Using this method you can perform a chain of transformations on a signal, and the transformed chain can then be sent on to other `NodeProxies`, in the manner described in `5.2`.

There are a few things to bear in mind when using this in performance, which are detailed in the examples file for this section, along with a list of suggested effects and some less conventional examples of how to use `\filterIn`.

Thanks to @datamads, @lvm and @cappelnord for the tip

### Using an effects `NodeProxy`
